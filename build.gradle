plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "io.github.goooler.shadow" version "8.1.8"
}

group = 'pl.tenfajnybartek'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
        }
    maven {
        name = 'sonatype-releases'
        url = uri('https://oss.sonatype.org/content/repositories/releases/')
    }
    maven {
        url = uri('https://jitpack.io')
    }
    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    compileOnly 'net.kyori:adventure-api:4.14.0'
    compileOnly "net.kyori:adventure-platform-bukkit:4.4.0"
    compileOnly 'me.clip:placeholderapi:2.11.6'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'org.xerial:sqlite-jdbc:3.45.0.0'
}

shadowJar {
    archiveClassifier.set('')
    relocate 'com.zaxxer.hikari', 'pl.tenfajnybartek.libs.hikari'
    relocate 'com.mysql', 'pl.tenfajnybartek.libs.mysql'
    relocate 'org.sqlite', 'pl.tenfajnybartek.libs.sqlite'
    minimize()
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}


tasks {
    runServer {
        minecraftVersion("1.21")
    }
}
tasks.build.dependsOn tasks.shadowJar

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}